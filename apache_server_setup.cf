bundle common apache_server
{
  vars:
    "template_dir"
      string => "/var/www/templates",
      comment => "Directory of templates to render source files";
}

bundle agent apache_server_setup
{
  methods:
    data:apache_server::
      "apache_server_dependencies"
        comment => "Install Apache 2 dependencies";
      "apache_server_fetch_sources"
        comment => "Fetch website sources from hub";
      "apache_server_render_sources"
        comment => "Render website sources into /var/www/html/";
      "apache_server_perms"
        comment => "Set permissions on directories";
}

bundle agent apache_server_dependencies
{
 packages:
   debian|ubuntu::
     "apache2"
       policy => "present",
       package_module => apt_get,
       comment => "Install Apache 2 from apt repository";
}

bundle agent apache_server_fetch_sources
{
  git:
    "web_sources"
      repository => "https://github.com/larsewi/cf3-website.git",
      destination => "$(apache_server.template_dir)",
      version => ifelse("data:prod_host", "production", "master"),
      update => "true",
      force => "true";
}

bundle agent apache_server_render_sources
{
  vars:
    "template_files"
      slist => findfiles("$(apache_server.template_dir)/src/**/*.{html,css}");

  files:
    "/var/www/html/$(with)"
      template_method => "cfengine",
      edit_template => "$(template_files)",
      with => string_replace("$(template_files)", "$(apache_server.template_dir)/src/", ""),
      comment => "Render source files from /var/www/template/ into /var/www/html/",
      perms => m(644);
}

bundle agent apache_server_perms
{
  files:
    "/var/www/html/."
      perms => m(755);

    "/var/www/html/css/."
      perms => m(755);
}
